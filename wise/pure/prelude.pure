export PURE_STACK=1024;

def extends x = x . reduce_with __locals__;

/* ---------------------
  Evaluation Functions 
--------------------- */

prefix 1000 evalb; // Evaluation to boolean
prefix 1000 evali; // Evaluation to integer
prefix 1000 evalf; // Evaluation to float
prefix 1000 evals; // Evaluation to symbolic value

/* ------------------------
  Prefix-Infix Conversion
------------------------ */

// For all x: i2p . p2i x = p2i . i2p x

// Hack to overcome the fact that we can't overload the internal
// `neg` function for integers
i2p x::double |
i2p x::int    = x if x > 0;
              = Neg $ abs x otherwise;

// Convert Infix to Prefix
i2p X = reduce X with
    x + y = add (i2p x) (i2p y);
    x - y = add (i2p x) (Neg $ i2p y);
    x * y = mul (i2p x) (i2p y);
    x / y = rational (i2p x) (i2p y);
    x ^ y = powr (i2p x) (i2p y);
    - x   = Neg (i2p x);
end;

// Convert Prefix to Infix
p2i X = reduce X with
    Num x         =    x   ;
    Neg x         =  - x   ;
    add x y       =  x + y ;
    mul x y       =  x * y ;
    rational x y  =  x / y ;
    powr x y      =  x ^ y ;

    // Disregard partial evaluations
    evalb x |
    evali x |
    evalf x |
    evals x = x;
end;

/* ------------------------
  Substitution
------------------------ */

using dict;
subst x e = subst x (hdict e) with
  subst (x@_ y) e = (subst x e) (subst y e);
  subst x e = if member e x then e!x else x;
end;

/* --------------------------
  Expression Tree Properties
---------------------------- */

//Check if an expression `x` has a subatom `e`.
has x e = detect x e with
    detect (x@_ y) e = (detect x e) || (detect y e);
    detect x e = 1 if (x === e) && atomp x;
               = 0 otherwise;
end;

// Walk the expression tree `x` and return 1 if any element has
// the given property `p`.
hasprop x p = detect x e with
    detect (x@_ y) e = (detect x e) || (detect y e);
    detect x e = 1 if (p x === 1);
               = 0 otherwise;
end;

//Check if an expression `x` has a subexpression `e`.
contains x e = detect x
with
    detect (func@_ a) = ((func$a) === e) || (detect func) || (detect a);
    detect x = 1 if x === e;
             = 0 otherwise;
end;

/* --------------------------
  Overload Default Arithmetic
---------------------------- */

def a::int/b::int       = rational a b;
def a::double/b::double = rational a b;
def a::int/b::double = rational a b;
def a::double/b::int = rational a b;

prefix 1001 `;

` expr = 1 if (expr === true);
       = 0 otherwise;

/* --------------------------
  Dynamic Rule Generation
---------------------------- */

// The prototype operator is used to initialize rules dynamially
infixl 1001 -->;
prototype x y = x --> y;

// Extract the lhs/rhs of a prototype
lhs (l --> r) = str l;
rhs (l --> r) = str r;

// Create an instance of our prototype rule
instance proto = eval $ (lhs proto) + " = " + (rhs proto);
